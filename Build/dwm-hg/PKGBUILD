# Maintainer: jasonwryan
# Using the patch queue extension

pkgname=dwm-hg
pkgver=1605
pkgrel=1
pkgdesc="a dynamic window manager for X"
url="http://dwm.suckless.org"
license=('custom:MIT/X')
arch=('i686' 'x86_64')
depends=('libxinerama')
makedepends=('mercurial')
optdepends=('dmenu')
conflicts=('dwm')
provides=('dwm')

_hgroot='http://code.suckless.org/hg'
_hgrepo='dwm'

build() {
  rm -rf "$srcdir/$_hgrepo-build"
  cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"
	
  # apply patchset
  if [ -d "$srcdir/$_hgrepo/.hg/patches" ]; then
      msg "Pushing queued patchset"
      hg qpush -a
  fi

  # customize Makefile
  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/^CFLAGS = -g/#CFLAGS += -g/g' config.mk
  sed -i 's/^#CFLAGS = -std/CFLAGS += -std/g' config.mk
  sed -i 's/^LDFLAGS = -g/#LDFLAGS += -g/g' config.mk
  sed -i 's/^#LDFLAGS = -s/LDFLAGS += -s/g' config.mk

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$srcdir/$_hgrepo-build"
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D README $pkgdir/usr/share/doc/$pkgname/README
}
