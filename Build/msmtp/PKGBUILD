# $Id: PKGBUILD 158094 2012-05-02 20:46:41Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Ben Mazer <blm@groknil.org>

pkgbase=msmtp
pkgname=('msmtp' 'msmtp-mta')
pkgver=1.4.28
pkgrel=1
groups=('modified')
arch=('i686' 'x86_64')
license=('GPL3')
url="http://msmtp.sourceforge.net"
makedepends=('gsasl')
source=(http://download.sourceforge.net/sourceforge/msmtp/${pkgbase}-${pkgver}.tar.bz2)
sha1sums=('3fd44b30e8f4ae071b2a5a205d6007f3465fa970')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --with-ssl=gnutls --without-gnome-keyring
  make
  make -C doc html pdf
}

package_msmtp() {
  pkgdesc="A mini smtp client"
  depends=('gsasl')
  install=msmtp.install

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install 
  make DESTDIR="${pkgdir}" -C doc install-html install-pdf

# Installing example configs and scripts to /usr/share/doc/msmtp
# as they are not installed by default (Debian and Gentoo do it this way)
  install -d "${pkgdir}/usr/share/doc/msmtp"
  cp -r scripts/{find_alias,msmtp-gnome-tool,msmtpqueue,msmtpq,set_sendmail} "${pkgdir}/usr/share/doc/msmtp/"
  install -D -m644 doc/*.example "${pkgdir}/usr/share/doc/msmtp/"

  install -D -m644 scripts/vim/msmtp.vim "${pkgdir}/usr/share/vim/vimfiles/syntax/msmtp.vim"
}

package_msmtp-mta() {
  pkgdesc="A mini smtp client - the regular MTA"
  depends=('msmtp')
  provides=('smtp-forwarder')

  install -d "${pkgdir}/usr/sbin"
  ln -s /usr/bin/msmtp "${pkgdir}/usr/sbin/sendmail"
}
