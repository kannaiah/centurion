# Build without texlive and gnome-keyring

pkgbase=msmtp
pkgname=('msmtp' 'msmtp-mta')
pkgver=1.4.31
pkgrel=4
arch=('i686' 'x86_64')
groups=('modified')
license=('GPL3')
url="http://msmtp.sourceforge.net"
makedepends=('gsasl')
source=(http://download.sourceforge.net/sourceforge/msmtp/${pkgbase}-${pkgver}.tar.bz2)
md5sums=('792ac0ffa30dc95ea1889a548128186d')

prepare() {
  cd ${pkgbase}-${pkgver}
  sed -i 's|/usr/bin/python|/usr/bin/python2|' scripts/msmtp-gnome-tool/msmtp-gnome-tool.py
}

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --with-ssl=gnutls --without-gnome-keyring
  make
  if [[ -n $(expac -Q '%n' texlive-core) ]]; then
      make -C doc html pdf
  fi
}

package_msmtp() {
  pkgdesc="A mini smtp client"
  depends=('gsasl')
  install=msmtp.install

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install 

  if [[ -n $(expac -Q '%n' texlive-core) ]]; then
  make DESTDIR="${pkgdir}" -C doc install-html install-pdf
  fi

# Installing example configs and scripts to /usr/share/doc/msmtp
# as they are not installed by default (Debian and Gentoo do it this way)
  install -d "${pkgdir}/usr/share/doc/msmtp"
  cp -r scripts/{find_alias,msmtp-gnome-tool,msmtpqueue,msmtpq,set_sendmail} "${pkgdir}/usr/share/doc/msmtp/"
  install -D -m644 doc/*.example "${pkgdir}/usr/share/doc/msmtp/"

  install -D -m644 scripts/vim/msmtp.vim "${pkgdir}/usr/share/vim/vimfiles/syntax/msmtp.vim"
}

package_msmtp-mta() {
  pkgdesc="A mini smtp client - the regular MTA"
  depends=('msmtp')
  provides=('smtp-forwarder')

    install -d "${pkgdir}"/usr/bin
    ln -s msmtp "${pkgdir}/usr/bin/sendmail"
}
